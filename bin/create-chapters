#!/usr/bin/env zsh

# https://stmorse.github.io/journal/Thesis-writeup.html

# First we link to the text that comprises each standalone
# chapter. (Note: we assume that the bibliography in each chapter
# is complete and don't try to add to it ourselves)

PROJECTS=$HOME/Development/
SYRTIS=$PROJECTS/Syrtis/versioned/Papers

cd $PROJECT_DIR
chapters=$PROJECT_DIR/chapters
ln -s $PROJECTS/Xenoliths/versioned/Paper $chapters/02-xenoliths
ln -s "$SYRTIS/Orientation Statistics" $chapters/04-statistics
ln -s "$SYRTIS/Syrtis Major" $chapters/05-syrtis
ln -s "$PROJECTS/Naukluft/versioned/Paper" $chapters/03-naukluft

build="build"
mkdir -p build

source paper-components/defs.zsh

G="\e[32m"
N="\e[0m"
P="\e[0;34m"

# First get chapter text into latex format
for chapter in chapters/*; do

  # Skip non-directories
  [[ ! -d $chapter ]] && continue
  name=${chapter:t}
  echo "\n\e[1;92m"$name$N

  build=build/$name
  includes=$build/includes
  # Appendices
  appendices=($chapter/text/appendices/*.md(N))

  mkdir -p $build

  # Get the directory from which included files should be sourced from
  eval include_dir="$(cat chapters/include-directories.json | jq ".[\"$name\"]")"

  if [[ -d $include_dir ]]; then
    echo "Copying figures and tables from $G$include_dir$N"
    rsync -a --delete $include_dir/ $includes
  else
    echo "Could not find include directory \e[0;31m$include_dir$N"
  fi

  echo $P"...building template$N"
  cp source/chapter-template.tex "$build/main.tex"
  (( ${#appendices} > 0 )) && cat source/appendices-shim.tex >> $build/main.tex
  # Replace template variables
  sed -i 's|\\$build|'"$build"'|' $build/main.tex

  echo $P"...copying title block and references$N"
  cp $chapter/text/title-block.tex $chapter/text/references.bib $build

  echo $P"...building figure captions$N"
  captions=$build/captions.tex
  cat $chapter/text/figure-captions.md \
  | text-pipeline \
  > $captions

  echo $P"...building abstract$N"
  cat $chapter/text/abstract.md \
  | text-pipeline \
  >> $build/abstract.tex

  function process-text {
    aggregate-text $@ \
    | mark-inline-figures \
    | text-pipeline \
    | sed -r 's/^\\section\{(Introduction)\}/\\invisiblesection\{\1\}/g' \
    | figurator inline \
      $chapter/text/includes.yaml \
      --template-dir paper-components/includes \
      --captions $captions \
      --collect-dir $include_dir
  }

  echo $P"...building text (and interleaving figures)$N"
  process-text $chapter/text/chapters/*.md > $build/body.tex

  if (( ${#appendices} > 0 )); then
    echo $P"...building appendices"$N
    process-text ${appendices} > $build/appendices.tex
  fi

  echo ""
done


