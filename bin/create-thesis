#!/usr/bin/env zsh

# https://stmorse.github.io/journal/Thesis-writeup.html

# First we link to the text that comprises each standalone
# chapter. (Note: we assume that the bibliography in each chapter
# is complete and don't try to add to it ourselves)

cd $PROJECT_DIR

source paper-components/defs.zsh

G="\e[32m"
N="\e[0m"
P="\e[0;34m"
BP="\e[1;94m"

function step {
  echo "$P...$@"$N
}

function directory-hash {
  gfind $@ -maxdepth 2 -type d -printf "%p %TY-%Tm-%Td %TH:%TM:%TS %Tz" | md5sum
}

echo $BP"BUILD CHAPTERS"$N
# First get chapter text into latex format
for chapter in chapters/*; do
  name=${chapter:t}
  # Skip non-directories
  if [[ ! -d $chapter ]]; then
    # Continue unless markdown
    [[ ${chapter:e} != "md" ]] && continue
    name=${name:r}
  fi
  echo "\e[1;92m"$name$N
  build=build/$name
  includes=$build/includes
  captions=$build/captions.tex
  # Appendices
  appendices=($chapter/appendices/*.md(N))
  hashfile="$build/.modification-hash"

  mkdir -p $build

  # Get the directory from which included files should be sourced from
  eval include_dir="$(cat chapters/include-directories.json | jq ".[\"$name\"]")"

  if [[ -d $include_dir ]]; then
    function process-text {
      aggregate-text $@ \
      | mark-inline-figures \
      | text-pipeline \
      | implicit-introduction \
      | figurator inline \
        $chapter/includes.yaml \
        --template-dir paper-components/includes \
        --captions $captions \
        --collect-dir $includes
    }

    _hash=$(directory-hash $chapter $include_dir)

  else
    function process-text {
      aggregate-text $@ \
      | text-pipeline \
      | implicit-introduction
    }
    _hash=$(directory-hash $chapter)
  fi
  if [[ -f "$hashfile" ]] && [[ $(cat $hashfile) == $_hash ]]; then
    echo "No changes"
    continue
  fi

  if [[ -d $include_dir ]]; then
    echo "Copying figures and tables from $G$include_dir$N"
    rsync -a --delete $include_dir/ $includes
  else
    if [[ $include_dir != null ]]; then
      echo "\e[0;31mCould not find include directory$N$include_dir"
    else
      echo "\e[0;31mNo include directory specified$N"
    fi
    echo "   (figures and tables will not print)"
  fi

  if [[ -d $chapter ]]; then
    step "building template"
    cp source/chapter-template.tex "$build/main.tex"
    (( ${#appendices} > 0 )) && cat source/appendices-shim.tex >> $build/main.tex
    # Replace template variables
    sed -i 's|\\$build|'"$build"'|' $build/main.tex

    step "copying title block and references"
    cp $chapter/title-block.tex $chapter/references.bib $build

    step "building abstract"
    cat $chapter/abstract.md \
    | text-pipeline \
    > $build/abstract.tex

    step "building figure captions"
    cat $chapter/figure-captions.md \
    | text-pipeline \
    > $captions

    step "building main text"
    process-text $chapter/chapters/*.md > $build/body.tex

    if (( ${#appendices} > 0 )); then
      step "building appendices"
      process-text ${appendices} > $build/appendices.tex
    fi
  else
    step "building main text"
    process-text $chapter > $build/main.tex
  fi

  # To save time next time
  echo $_hash > $hashfile

done

echo $BP"BUILD THESIS"$N
texfile=build/main.tex
preamble="build/preamble.tex"
cp thesis-template/* build
cp source/main.tex $texfile
cat "paper-components/defs/preamble-thesis"/*.tex > $preamble
step "building template"


step "running latex in draft mode"
cp $BIBTEX_LIBRARY build/references.bib
run-latex-draft $texfile build/Daven-Quinn-thesis-draft.pdf > "build/latex-draft.log"

step "running bibtex"
for chapter in chapters/*; do
  name=${chapter:t:r}
  [[ -f $chapter ]] && [[ ${chapter:e} != 'md' ]] && continue
  bibtex build/$name/main.aux
done

bibtex build/Daven-Quinn-thesis-draft.aux

run-latex $texfile build/Daven-Quinn-thesis-draft.pdf > "build/latex-final.log"

